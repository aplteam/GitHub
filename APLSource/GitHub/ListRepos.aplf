 r←ListRepos parms;G;list;qdmx
⍝
 :Trap 11
     G←⎕NEW GitHubAPIv3(,⊂parms.owner)
     G.personal_access_token←parms.accessToken
 :Else
     0 Assert⍨'Owner <',parms.owner,'> is unknown'
 :EndTrap
 :Trap 11
     list←G.GetAllRepos
 :Else
     qdmx←⎕DMX
     ('<',parms.owner,'> unknown (404)')Assert'Not Found (404)'≢qdmx.EM
     qdmx.EM Assert 0
 :EndTrap
 :If 0=≢list
     r←''
 :Else
     :If parms.verbose
         r←↑list.(name open_issues_count description)
         :If parms.issues
             r←(0<list.open_issues_count)⌿r
         :EndIf
         r[⍸(⊂'null')∘≡¨r[;3];3]←' '
         :If 0<≢parms.sort
             :If parms.sort≡'name'
                 r←r[⍋r[;1];]
             :ElseIf parms.sort≡'issues'
                 r←r[⍒r[;2];]
             :Else
                 ∘∘∘ ⍝ Huh?!
             :EndIf
         :EndIf
         r←({⍵,[0.5]'-'⍴⍨¨≢¨⍵}'Name' 'OI (*)' 'Description'),[1]r
         r⍪←↑(' ' ' ' ' ')('* Open issues' ' ' ' ')
         r[⍸0≡¨r[;2];2]←⊂''
     :Else
         :If parms.issues
             r←(0<list.open_issues_count)⌿⍪list.name
         :Else
             r←⍪list.name
         :EndIf
         r←({⍵,[0.5]'-'⍴⍨¨≢¨⍵},⊂'Name (',(⍕≢r),')'),[1]r
     :EndIf
 :EndIf
